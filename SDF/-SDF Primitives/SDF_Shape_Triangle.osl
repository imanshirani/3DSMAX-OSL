// SDF_Shape_Triangle.osl by Iman Shirani
// 2025-11-25

shader SDF_Shape_Triangle
(
    point Position = P,
    point P1 = point(0.0, 0.5, 0.0),
    point P2 = point(0.5, -0.5, 0.0),
    point P3 = point(-0.5, -0.5, 0.0),
    float CornerRadius = 0.0 [[ string label = "Corner Radius", float min = 0.0 ]],
    output float SDF_Out = 0.0
)
{
    vector p = vector(Position[0], Position[1], 0.0);
    vector p0 = vector(P1[0], P1[1], 0.0);
    vector p1 = vector(P2[0], P2[1], 0.0);
    vector p2 = vector(P3[0], P3[1], 0.0);
    
    vector e0 = p1 - p0; vector v0 = p - p0;
    vector e1 = p2 - p1; vector v1 = p - p1;
    vector e2 = p0 - p2; vector v2 = p - p2;
    
    vector pq0 = v0 - e0*clamp(dot(v0,e0)/dot(e0,e0), 0.0, 1.0);
    vector pq1 = v1 - e1*clamp(dot(v1,e1)/dot(e1,e1), 0.0, 1.0);
    vector pq2 = v2 - e2*clamp(dot(v2,e2)/dot(e2,e2), 0.0, 1.0);
    
    float s = sign(e0[0]*e2[1] - e0[1]*e2[0]);
    
    vector d_min = min(min(vector(dot(pq0,pq0), s*(v0[0]*e0[1]-v0[1]*e0[0]), 0.0),
                           vector(dot(pq1,pq1), s*(v1[0]*e1[1]-v1[1]*e1[0]), 0.0)),
                           vector(dot(pq2,pq2), s*(v2[0]*e2[1]-v2[1]*e2[0]), 0.0));
    
    float d = -sqrt(d_min[0]) * sign(d_min[1]);
    
    SDF_Out = d - CornerRadius;
}