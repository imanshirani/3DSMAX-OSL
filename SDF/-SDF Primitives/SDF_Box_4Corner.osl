// SDF_Box_4Corner.osl  by IMAN SHIRANI
// 2025-11-25

shader SDF_Box_4Corner
(
    point Position = P,
    
    float Width = 1.0 [[ string label = "Width", float min = 0.0 ]],
    float Height = 1.0 [[ string label = "Height", float min = 0.0 ]],
    
    
    float R_TopRight = 0.2 [[ string label = "Radius Top-Right", float min = 0.0 ]],
    float R_TopLeft = 0.0 [[ string label = "Radius Top-Left", float min = 0.0 ]],
    float R_BotRight = 0.0 [[ string label = "Radius Bot-Right", float min = 0.0 ]],
    float R_BotLeft = 0.2 [[ string label = "Radius Bot-Left", float min = 0.0 ]],
    
    output float SDF_Out = 0.0
)
{
    
    point p = Position;
    
    
    vector b = vector(Width * 0.5, Height * 0.5, 0.0);
    
    
    float r = (p[0] > 0.0) ? 
              ((p[1] > 0.0) ? R_TopRight : R_BotRight) : 
              ((p[1] > 0.0) ? R_TopLeft  : R_BotLeft);
              
    
    vector q = abs(p) - b + vector(r, r, 0.0);
    
    
    float d = min(max(q[0], q[1]), 0.0) + length(max(q, 0.0));
    
    
    SDF_Out = d - r;
}